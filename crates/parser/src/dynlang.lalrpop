use std::string::String;
use crate::lexer::{Token, LexError};
use crate::ast::*;
use tap::Tap;
use span::Span;

grammar;

// macros
Separated<T, U>: Vec<T> = ((<T> U)* T)? => <>.map(|(v, l)| v.tap_mut(|v| v.push(l))).unwrap_or(vec![]);

Punctuated<T, U>: Vec<T> = {
    <v:(<T> U)*> <l:T?> => match l {
		Some(l) => v.tap_mut(|v| v.push(l)),
        None => v,
    }
};

// nil
Nil: Nil = <l:@L> "nil" <r:@R> => Nil::new(l..r);

// bool
Boolean: Boolean = {
	<l:@L> "true" <r:@R> => Boolean::new(true, l..r),
	<l:@L> "false" <r:@R> => Boolean::new(false, l..r),
};

// int
Integer: Integer = "int" => Integer(<>);

// string
StringT: StringT = "string" => StringT(<>);

// literal
Literal: Literal = {
	Nil => Literal::Nil(<>),
	Boolean => Literal::Boolean(<>),
	Integer => Literal::Integer(<>),
	StringT => Literal::String(<>),
};

// identifier
Ident: Ident = <l:@L> <i:"ident"> <r:@R> => Ident::new_box(Span::new(l, r), i);

// atom
L0: Expr = {
	Literal => Expr::Literal(<>),
	Ident => Expr::Ident(<>),
	"(" <Expr> ")",
	"[" <Elements> "]" => Expr::Array(Array(Code(<>))),
	"|" <parameters:Parameters> "|" "->" "{" <body:Code> "}" => Expr::Function(Function {
		parameters,
		body,
	}),
	"||" "->" "{" <Code> "}" => Expr::Function(Function {
		parameters: Parameters(vec![]),
		body: <>,
	}),
};

L1 = {
	<L1> "[" <Expr> "]" => Expr::BinExpr(BinExpr::index_box(<>)),
	<L1> "(" <Arguments> ")" => Expr::call_box(<>),
	L0,
}

L2 = {
	"-" <L2> => Expr::unary_minus_box(<>),
	"!" <L2> => Expr::unary_not_box(<>),
	L1,
}

L3 = {
	<L3> "*" <L2> => Expr::BinExpr(BinExpr::mul_box(<>)),
	<L3> "/" <L2> => Expr::BinExpr(BinExpr::div_box(<>)),
	<L3> "%" <L2> => Expr::BinExpr(BinExpr::mod_box(<>)),
	L2,
}

L4 = {
	<L4> "+" <L3> => Expr::BinExpr(BinExpr::add_box(<>)),
	<L4> "-" <L3> => Expr::BinExpr(BinExpr::sub_box(<>)),
	L3,
};

L5 = {
	<L5> "==" <L4> => Expr::BinExpr(BinExpr::equal_box(<>)),
	<L5> "!=" <L4> => Expr::BinExpr(BinExpr::not_equal_box(<>)),
	<L5> "<" <L4> => Expr::BinExpr(BinExpr::less_than_box(<>)),
	<L5> "<=" <L4> => Expr::BinExpr(BinExpr::less_than_equal_box(<>)),
	<L5> ">" <L4> => Expr::BinExpr(BinExpr::greater_than_box(<>)),
	<L5> ">=" <L4> => Expr::BinExpr(BinExpr::greater_than_equal_box(<>)),
	L4,
};

L6 = {
	<L6> "&&" <L5> => Expr::BinExpr(BinExpr::and_box(<>)),
	L5,
};

L7 = {
	<L7> "||" <L6> => Expr::BinExpr(BinExpr::or_box(<>)),
	L6,
};

L8 = {
	"let" <i:Ident> "=" <e:L8> => Expr::Declare(i, Box::new(e)),
	"let!" <i:Ident> "=" <e:L8> => Expr::DeclareMut(i, Box::new(e)),
	<i:Ident> "=" <e:L8> => Expr::Assign(i, Box::new(e)),
	<i:Ident> "+=" <e:L8> => Expr::AddAssign(i, Box::new(e)),
	<i:Ident> "-=" <e:L8> => Expr::SubAssign(i, Box::new(e)),
	<i:Ident> "*=" <e:L8> => Expr::MulAssign(i, Box::new(e)),
	<i:Ident> "/=" <e:L8> => Expr::DivAssign(i, Box::new(e)),
	<i:Ident> "%=" <e:L8> => Expr::ModAssign(i, Box::new(e)),
	"return" <L8> => Expr::return_box(<>),
	"break" <L8> => Expr::break_box(<>),
	"continue" <L8> => Expr::continue_box(<>),
	"panic" <L8> => Expr::panic_box(<>),
	"assert" <L8> => Expr::assert_box(<>),
	"if" <c:L8> "{" <b:Code> "}" => Expr::If { condition: Box::new(c), yes: b },
	"if" <c:L8> "{" <t:Code> "}" "else" "{" <f:Code> "}" => Expr::IfElse {
		condition: Box::new(c),
		yes: t,
		no: f
	},
	"iter" <a:L8> "of" <i:Ident> "{" <b:Code> "}" => Expr::For {
		collection: Box::new(a),
		item: i,
		body: b
	},
	"|" <parameters:Parameters> "|" "->" <e:L8> => Expr::Function(Function {
		parameters,
		body: Code(vec![e]),
	}),
	"||" "->" <e:L8> => Expr::Function(Function {
		parameters: Parameters(vec![]),
		body: Code(vec![e]),
	}),
	L7,
};

pub Expr = {
	L8,
}

ElementSeparator = { ",", "\n" };

Elements = Punctuated<Expr, ElementSeparator>;

Arguments: Arguments = <Elements> => Arguments(<>);

Parameters: Parameters = Punctuated<Ident, ElementSeparator> => Parameters(<>);
	
pub Code: Code = Punctuated<Expr, "\n"+> => Code(<>);

extern {
	type Location = usize;
	type Error = LexError;

	enum Token {
		"+" => Token::Plus,
		"-" => Token::Minus,
		"*" => Token::Asterisk,
		"/" => Token::Slash,
		"%" => Token::Percent,
		"=" => Token::Assign,
		"+=" => Token::PlusAssign,
		"-=" => Token::MinusAssign,
		"*=" => Token::AsteriskAssign,
		"/=" => Token::SlashAssign,
		"%=" => Token::PercentAssign,
		"==" => Token::Equal,
		"!=" => Token::NotEqual,
		"<" => Token::LeftAngledBracket,
		">" => Token::RightAngledBracket,
		"<=" => Token::LessThanEqual,
		">=" => Token::GreaterThanEqual,
		"&&" => Token::DoubleAnd,
		"||" => Token::DoublePipe,
		"(" => Token::LeftParenthesis,
		")" => Token::RightParenthesis,
		"{" => Token::LeftBrace,
		"}" => Token::RightBrace,
		"[" => Token::LeftBracket,
		"]" => Token::RightBracket,
		"!" => Token::Bang,
		"." => Token::Dot,
		"," => Token::Comma,
		"|" => Token::Pipe,
		"@" => Token::At,
		"->" => Token::Arrow,
		"nil" => Token::Nil,
		"true" => Token::True,
		"false" => Token::False,
		"panic" => Token::Panic,
		"assert" => Token::Assert,
		"let" => Token::Let,
		"let!" => Token::LetMut,
		"if" => Token::If,
		"else" => Token::Else,
		"iter" => Token::Iter,
		"of" => Token::Of,
		"return" => Token::Return,
		"break" => Token::Break,
		"continue" => Token::Continue,
		"import" => Token::Import,
		"export" => Token::Export,
		"\n" => Token::NewLine,
		// "lcomment" => Token::LineComment,
		// "bcomment" => Token::BlockCommnet,
		"int" => Token::Integer(<i32>),
		"string" => Token::String(<String>),
		"ident" => Token::Identifier(<String>),
	}
}


